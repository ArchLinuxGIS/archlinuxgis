# Maintainer: Lantald  <lantald at Gmx dot com>
# Contributor: Emiliano Vavassori <syntaxerrormmm at gmail dot com>
pkgname=spatialindex
pkgver=1.7.1
pkgrel=1
pkgdesc="An extensible framework that supports robust spatial indexing methods and sophisticated spatial queries."
arch=('i686' 'x86_64')
url="http://libspatialindex.github.com/"
license=('LGPL')
depends=('gcc-libs')
provides=('spatialindex')
conflicts=('libspatialindex-git')
source=("http://download.osgeo.org/libspatialindex/${pkgname}-src-${pkgver}.tar.gz")
md5sums=('8599243d5d8204f0f8d92cd55ab120f5')

build() {
  cd "${srcdir}/${pkgname}-src-${pkgver}/"
  ./configure --prefix=/usr
  make
}

check() {
  cd "${srcdir}/${pkgname}-src-${pkgver}/"
  make -k check
}

package() {
  cd "${srcdir}/${pkgname}-src-${pkgver}/"
  make DESTDIR="${pkgdir}/" install
}
